buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.1.5'
        
    }
}

plugins {
    id 'checkstyle'
    id 'distribution'
    id 'java-library'
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "com.github.johnrengelman.shadow" version "7.0.0" // Updated version
    id "kr.motd.sphinx" version "2.5.0"
    id "jacoco"
    id "org.owasp.dependencycheck" version "6.1.5"
}

group 'com.opendxl'

//sourceCompatibility = '17'

apply plugin: 'base'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    suppressionFile = file("${projectDir}/config/dependency-check/owasp-suppression.xml")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = "0.8.6"
    reportsDir = file("$buildDir/reports/coverage")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("$buildDir/reports/coverage")
    }
}

configurations {
    kafkaInMemory
    sampleJars.extendsFrom testImplementation
    // Added below to get away of VSCode complaints - This might need only on local
    // Comment it out before generating final artifacts 
    implementation.extendsFrom kafkaInMemory
}

dependencies {
    implementation ('org.apache.kafka:kafka-clients:3.1.2') {
        exclude group: 'org.scala-lang', module: 'scala-reflect'
        exclude group: 'org.lz4', module: 'lz4-java'
    }
    implementation ('org.apache.kafka:kafka-streams:3.1.2') {
        exclude group: 'org.scala-lang', module: 'scala-reflect'
    }
    implementation 'org.scala-lang:scala-reflect:2.13.8'
    implementation 'org.lz4:lz4-java:1.8.0'
    implementation 'org.apache.avro:avro:1.11.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.apache.commons:commons-configuration2:2.8.0'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'

    testImplementation('org.apache.kafka:kafka_2.13:3.1.2') {
        exclude group: 'org.scala-lang', module: 'scala-reflect'
    }
    testImplementation 'org.scala-lang:scala-reflect:2.13.8'
    testImplementation 'org.apache.zookeeper:zookeeper:3.5.7'
    testImplementation('io.netty:netty-all:4.1.91.Final') {
        force = true
    }
    testImplementation 'commons-io:commons-io:2.11.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    testImplementation 'com.e-movimento.tinytools:privilegedaccessor:1.2.2'

    kafkaInMemory ('org.apache.kafka:kafka_2.13:3.1.2') {
        exclude group: 'org.scala-lang', module: 'scala-reflect'
        exclude group: 'org.lz4', module: 'lz4-java'
        exclude group: 'io.netty'
    }
    kafkaInMemory 'org.scala-lang:scala-reflect:2.13.8'
    kafkaInMemory 'org.lz4:lz4-java:1.8.0'
    kafkaInMemory 'commons-io:commons-io:2.11.0'

    // This following section mitigates OWASP vulnerabilities report.
    // It enforces to use specific transitive dependency versions
    // when their previous versions were mentioned by OWASP vulnerability reports.
    // If a vulnerability is false-positive,
    // libraries should be declared in /config/dependency-check/owasp-supression.xml

    // https://nvd.nist.gov/vuln/detail/CVE-2019-12384
    // https://nvd.nist.gov/vuln/detail/CVE-2019-12814
    // https://nvd.nist.gov/vuln/detail/CVE-2019-14379
    // https://nvd.nist.gov/vuln/detail/CVE-2019-14439
    // http://web.nvd.nist.gov/view/vuln/detail?vulnId=CVE-2019-14540
    // http://web.nvd.nist.gov/view/vuln/detail?vulnId=CVE-2019-16335
    // http://web.nvd.nist.gov/view/vuln/detail?vulnId=CVE-2019-16942
    // http://web.nvd.nist.gov/view/vuln/detail?vulnId=CVE-2019-16943
    // CVE-2020-25649
    // implementation ('com.fasterxml.jackson.core:jackson-databind:2.14.2') {
    //     force = true
    // }
    implementation 'com.fasterxml.jackson.module:jackson-module-scala_3:2.14.2'
    kafkaInMemory ('com.fasterxml.jackson.core:jackson-databind:2.14.2') {
        force = true
    }

    // http://web.nvd.nist.gov/view/vuln/detail?vulnId=CVE-2019-12402
    kafkaInMemory ('org.apache.commons:commons-compress:1.21') {
        force = true
    }
    implementation ('org.apache.commons:commons-compress:1.21') {
        force = true
    }
}

repositories {
    mavenCentral()
}

// Jar Manifest info
jar {
    manifest {
        attributes('Main-Class': 'com.opendxl.databus.cli.CommandLineInterface',
                'Implementation-Title': rootProject.name,
                'Implementation-Version': rootProject.name
        )
    }
}

// Task for creating a jar with the source files
task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE // Handle duplicates
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourceJar
        }
    }
}

// Task for creating a jar with JavaDoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    exclude "**/broker/**"
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

tasks.named('sphinx') {
    enabled = false
}


    //duplicatesStrategy = DuplicatesStrategy.EXCLUDE // Handle duplicates


// Set up the distribution zip
distributions {
    main {
        distributionBaseName = 'dxldatabusclient-java-sdk'
        contents {
            from(jar) {
                into('lib')
            }
            from(shadowJar) {
                into("lib")
            }
            from(configurations.sampleJars) {
                into("sample/lib")
            }
            from(configurations.compileClasspath) {
                into("lib/3rdparty")
            }
            from(sphinx) {
                into('doc')
            }
            from('sample') {
                into 'sample'
            }
            from('broker/src/broker') {
                into 'sample/src/broker'
            }
            from(shadowJar) {
                into("sample/lib")
            }
            from(configurations.kafkaInMemory) {
                into("sample/lib/kafka")
            }
            from('distribution')
        }
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/java'
        }
        compileClasspath +=   configurations.kafkaInMemory
        runtimeClasspath +=  configurations.kafkaInMemory
    }
    sample {
        java {
            srcDir 'sample/src'
            srcDir 'broker/src'
        }
        resources {
            srcDir 'sample/src'
        }
        compileClasspath += main.output + test.output + configurations.compileClasspath + configurations.kafkaInMemory
        runtimeClasspath += main.output + test.output + configurations.runtimeClasspath + configurations.kafkaInMemory
    }
    test {
        java {
            srcDir 'src/test/java'
            srcDir 'broker/src'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

// Make check style run before creating a jar
jar.dependsOn(checkstyleMain)

// Force distZip to be dependent on javadocJar and sourceJar
distZip.dependsOn javadocJar
distZip.dependsOn sourceJar

sphinx {
    // Change the source directory.
    sourceDirectory = "${projectDir}/docs"
    // Change the output directory.
    outputDirectory = "${project.buildDir}/docs"
}

task versionFile()  {
    doLast {
        new File("${projectDir}/VERSION").text = """$version"""
    }
}

checkstyle {
    sourceSets = []
    toolVersion '8.39'


}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            pom {
                name = 'OpenDXL Databus Java Client'
                description = 'An OpenDXL Databus client library for Java'
                url = 'https://github.com/opendxl/opendxl-databus-client-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        email = 'support@opendxl.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/opendxl/opendxl-databus-client-java.git'
                    developerConnection = 'scm:git:ssh://github.com/opendxl/opendxl-databus-client-java.git'
                    url = 'https://github.com/opendxl/opendxl-databus-client-java'
                }
            }

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
        }
    }
    repositories {
        maven {
            url = "$buildDir/repo"
//            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
//            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
//            credentials {
//                username sonatypeUsername
//                password sonatypePassword
//            }

        }
    }
}

task replaceVersionInREADME() {
    if (!version.endsWith('SNAPSHOT')) {
        // Maven
        ant.replaceregexp(match: '<version>(.+)</version>', replace: "<version>${version}</version>", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        ant.replaceregexp(match: '<version>(.+)</version>', replace: "<version>${version}</version>", flags: 'g', byline: true) {
            fileset(dir: 'docs', includes: 'index.rst')
        }
        // Gradle
        ant.replaceregexp(match: "com\\.opendxl\\:dxldatabusclient\\:(.+)", replace: "com.opendxl:dxldatabusclient:${version}'", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        ant.replaceregexp(match: "com\\.opendxl\\:dxldatabusclient\\:(.+)", replace: "com.opendxl:dxldatabusclient:${version}'", flags: 'g', byline: true) {
            fileset(dir: 'docs', includes: 'index.rst')
        }
        // Command Line (Docs)
        ant.replaceregexp(match: "dxldatabusclient-(.*).jar", replace: "dxldatabusclient-${version}.jar", flags: 'g', byline: true) {
            fileset(dir: 'docs', includes: 'CLI-Example.rst')
        }
    }
}

signing {sign publishing.publications.maven}
check.dependsOn dependencyCheckAnalyze
distZip.dependsOn javadocJar
distZip.dependsOn sourceJar
sphinx.dependsOn(versionFile)
assemble.dependsOn(replaceVersionInREADME)
